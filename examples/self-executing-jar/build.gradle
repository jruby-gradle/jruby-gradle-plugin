/*
 * This project is intended to be used as an acceptance test *and* a
 * documentation example. If you change this file, please be sure that it
 * renders appropriately in the generated documentation
 */
buildscript {
    repositories { jcenter() }

    dependencies {
        /* Replace "%%VERSION%%" with the version of JRuby/Gradle you wish to
         * use if you want to use this build.gradle outside of gradleTest
         */
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:%%VERSION%%'
        classpath 'com.github.jruby-gradle:jruby-gradle-jar-plugin:%%VERSION%%'
    }
}

apply plugin: "com.github.jruby-gradle.jar"

dependencies {
    /* Using the built-in `gems` configuration to describe the
     * dependencies our jrubyJar task will need, so the gem is properly
     * included in the resulting .jar file
     */
    gems "rubygems:colorize:0.7.7+"
}

jrubyJar {
    /* We want to use this Ruby script as our start point when the jar executes
     */
    initScript "${projectDir}/entrypoint.rb"
    /* make sure we use our gems from the default "gems" configuration! */
    defaultGems()
}



/*
 * This task is only here for the execution fo the gradleTest
 */
task runGradleTest(type: Exec) {
    dependsOn jrubyJar
    description "Execute the constructed self-executing jar"
    environment [:]
    workingDir "${buildDir}/libs"
    commandLine 'java', '-jar', jrubyJar.outputs.files.singleFile.absolutePath
}
