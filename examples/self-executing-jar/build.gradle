/*
 * This project is intended to be used as an acceptance test *and* a
 * documentation example. If you change this file, please be sure that it
 * renders appropriately in the generated documentation
 */
buildscript {
    repositories { jcenter() }

    dependencies {
        /* here to make sure that our dependencies get loaded in properly under
         * GradleTest, this is NOT needed by end-users
         */
        classpath 'com.github.jengelman.gradle.plugins:shadow:[7.0,8.0)'
    }
}

plugins {
   id 'com.github.jruby-gradle.jar'
}

repositories {
    jcenter()
    ruby.gems()
}

dependencies {
    /* Using the built-in `jrubyJar` configuration to describe the
     * dependencies our jrubyJar task will need, so the gem is properly
     * included in the resulting .jar file
     */
    jrubyJar "rubygems:colorize:0.7.7+"
    jrubyJar 'org.slf4j:slf4j-simple:1.7.12'
}

jrubyJar {
    /* We want to use this Ruby script as our start point when the jar executes
     */
    initScript "${projectDir}/entrypoint.rb"
}


/*
 * This task is only here for the execution fo the gradleTest
 */
task runGradleTest(type: Exec) {
    dependsOn jrubyJar
    description "Execute the constructed self-executing jar"
    environment [:]
    workingDir "${buildDir}/libs"
    commandLine 'java', '-jar', jrubyJar.outputs.files.singleFile.absolutePath
}
