
task prepareGroovyDocs(type : Copy) {

    description "Copy Groovydocs into our site directory for deploybment/publication"
    into "${buildDir}/_site/groovydoc"

    rootProject.pluginProjects.each {
        def task = it.tasks.findByName('groovydoc')

        if ((task) && (task.outputs.files)) {
            into(it.name) { from task.outputs.files }
        }
    }
    dependsOn rootProject.pluginProjects.collect { ":${it.name}:groovydoc" }
}

task compileAsciidoc( type : GradleBuild ) {
    buildFile = file('docs.gradle')
    tasks = ['compileAsciidoc']
    startParameter.projectProperties+= [
        PLUGIN_DIR : "${rootProject.projectDir}/jruby-gradle-base-plugin/build/libs",
        rubygemsProxy : rubygemsProxy
    ]

    startParameter.projectCacheDir = new File(
        gradle.startParameter.projectCacheDir ?
            "${gradle.startParameter.projectCacheDir}" : "${rootProject.projectDir}/.gradle",
        project.name.replace(":", "_")
    )

    dependsOn ':jruby-gradle-base-plugin:jar'
}

task assembleDocs {
    dependsOn compileAsciidoc, prepareGroovyDocs
}

githubPages {
    repoUri = 'git@github.com:jruby-gradle/jruby-gradle.github.io'
    targetBranch = 'master'
    pages {
        from "${buildDir}/_site"
        from "${rootProject.projectDir}/CNAME"
    }
}
publishGhPages.dependsOn assembleDocs

